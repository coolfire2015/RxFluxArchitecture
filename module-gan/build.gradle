if (isModuleAsApp.toBoolean()) apply plugin: 'com.android.application'
else apply plugin: 'com.android.library'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'com.github.dcendents.android-maven'

group = "com.github.coolfire2015"

android {
    //编译版本
    compileSdkVersion COMPILE_SDK_VERSION as int
    //lint设置
    lintOptions {
        //lint 不被终止
        abortOnError false
        //打Release版本时,不进行检测
        checkReleaseBuilds false
        //关闭检索的功能
        disable 'GoogleAppIndexingWarning'
    }
    //签名配置,自定义,用在defaultConfig中
    signingConfigs {
        debug {
            storeFile file(STORE_DEBUG_FILE)
        }
        release {
            storeFile file(STORE_RELEASE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }
    //默认配置
    defaultConfig {
        multiDexEnabled true//支持Multidex分包
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode rootProject.gitGitVersionCode()
        versionName rootProject.getGitVersionName()
        signingConfig signingConfigs.debug
        vectorDrawables.useSupportLibrary = true//使用vector图片
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName(),
                             eventBusIndex      : 'com.huyingbao.module.gan.GanEventBusIndex']
            }
        }
    }
    //构建过程
    buildTypes {
        //自定义buildType.name
        debug {
            //显示分支名字
            buildConfigField 'String', 'BRANCH', '"' + rootProject.getGitBranch() + '"'
            //单元测试mockUrl
            buildConfigField "boolean", "MOCK_URL", '' + isUseMockUrl.toBoolean() + ''
            //替换字符串资源
            resValue "string", "app_name", "干货Debug"
            //混淆开关
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled true
            //混淆文件
            proguardFile 'proguard-rules.pro'
            //签名配置
            signingConfig signingConfigs.debug
            //声明主 DEX 文件中需要的类
            //multiDexKeepProguard file('multidex-config.pro')
        }
        //自定义buildType.name
        release {
            //显示分支名字
            buildConfigField 'String', 'BRANCH', '"' + rootProject.getGitBranch() + '"'
            //替换字符串资源
            resValue "string", "app_name", "干货"
            //混淆开关
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled true
            //混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名配置
            signingConfig signingConfigs.release
        }
    }
    //数据绑定
    dataBinding {
        enabled = true
    }
    //Dex分包
    dexOptions {
        jumboMode = true
        javaMaxHeapSize "4g"//增加java堆内存大小
    }
    //Lambda表达式需要
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //单元测试,robolectric支持
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    //代码目录设置
    sourceSets {
        main {
            if (isModuleAsApp.toBoolean()) {
                manifest.srcFile 'src/app/AndroidManifest.xml'
                java.srcDirs 'src/app/java'
                res.srcDirs 'src/app/res'
            } else {
                manifest.srcFile 'src/library/AndroidManifest.xml'
            }
        }
    }
    //修改生成的apk名字
    if (isModuleAsApp.toBoolean()) {
        applicationVariants.all { variant ->
            variant.outputs.all {
                if ("release".equals(variant.buildType.name))
                    outputFileName = "demo_v_" + variant.buildType.name + "_" + rootProject.getGitVersionName() + '.apk'
            }
        }
    }
}

//将源代码打包进aar
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.javaDirectories
}

artifacts {
    archives sourcesJar
}

//仓库
repositories {
    //本地仓库,添加aar支持
    flatDir { dirs 'libs' }
}

dependencies {
    //通用依赖模块
    api project(':core-common')
    //编译时生成
    annotationProcessor project(':core-eventbus-processor')
    annotationProcessor "com.google.dagger:dagger-compiler:${DAGGER}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${DAGGER}"
    annotationProcessor "com.android.tools.build.jetifier:jetifier-core:${JETIFIER}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${BUTTER_KNIFE}"
    annotationProcessor "com.alibaba:arouter-compiler:${AROUTER_COMPILER}"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:${X_LIFECYCLE}"
}