if (isDebug.toBoolean()) apply plugin: 'com.android.application'
else apply plugin: 'com.android.library'

apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    //lint设置
    lintOptions {
        //lint 不被终止
        abortOnError false
        //打Release版本时,不进行检测
        checkReleaseBuilds false
    }
    //签名配置,自定义,用在defaultConfig中
    signingConfigs {
        debug {
            storeFile file(STORE_DEBUG_FILE)
        }
    }
    //默认配置
    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode rootProject.gitGitVersionCode()
        versionName rootProject.getGitVersionName()
        signingConfig signingConfigs.debug
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true//使用vector图片
    }
    //构建过程
    buildTypes {
        //自定义buildType.name
        debug {
            //混淆开关
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled true
            //混淆文件
            proguardFile 'proguard-rules.pro'
            //签名配置
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        main {
            if (isDebug.toBoolean()) {
                manifest.srcFile 'src/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/release/AndroidManifest.xml'
            }
        }
    }

    //Lambda表达式需要
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':core-arch')
    implementation project(':core-custom')

    api "com.android.support:cardview-v7:${SUPPORT_VERSION}"

    annotationProcessor "android.arch.lifecycle:compiler:${ARCH_LIFECYCLE_VERSION}"

    annotationProcessor "com.google.dagger:dagger-compiler:${DAGGER}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${DAGGER}"

    annotationProcessor "com.jakewharton:butterknife-compiler:${BUTTER_KNIFE}"
}