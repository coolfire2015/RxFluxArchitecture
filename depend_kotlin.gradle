//Kotlin业务模块通用依赖
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    //编译版本
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION
    //lint设置
    lintOptions {
        //lint 不被终止
        abortOnError false
        //打Release版本时,不进行检测
        checkReleaseBuilds false
        //关闭检索的功能
        disable 'GoogleAppIndexingWarning'
    }
    //构建功能
    buildFeatures {
        //支持 DataBinding
        dataBinding = true
    }
    //Dex分包
    dexOptions {
        jumboMode = true
        javaMaxHeapSize "4g"//增加java堆内存大小
    }
    //Java1.8支持
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //Kotlin Java1.8支持
    kotlinOptions {
        jvmTarget = "1.8"
    }
    //Kotlin 实验功能
    androidExtensions {
        experimental = true
    }
    //单元测试,Robolectric支持
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

//将源代码打包进aar
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.javaDirectories
}

artifacts {
    archives sourcesJar
}

//仓库
repositories {
    //本地仓库,添加aar支持
    flatDir { dirs 'libs' }
}

//Gradle可以缓存SNAPSHOT构建，以确保Gradle始终选择构建的“最新”版本：
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    //arch-base
    implementation project(':core-base')
    implementation project(':core-progress')
    kapt project(':core-arch-processor')
    kapt project(':core-eventbus-processor')
    //arch-test
    testImplementation project(':core-test')
    kaptTest project(':core-arch-processor')
    kaptTest project(':core-eventbus-processor')
    //X-Core
    implementation "androidx.core:core-ktx:${X_CORE}"
    //X_APPCOMPAT
    implementation "androidx.appcompat:appcompat:${X_APPCOMPAT}"
    //X-Fragment
    implementation "androidx.fragment:fragment:${X_FRAGMENT}"
    implementation "androidx.fragment:fragment-ktx:${X_FRAGMENT}"
    testImplementation "androidx.fragment:fragment-testing:${X_FRAGMENT}"
    //X-Lifecycle
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${X_LIFECYCLE}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${X_LIFECYCLE}"
    implementation "androidx.lifecycle:lifecycle-extensions:${X_LIFECYCLE}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${X_LIFECYCLE}"
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${KOTLIN_VERSION}"
    //Kotlin-协程
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${KOTLIN_COROUTINES}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${KOTLIN_COROUTINES}"
    //X-Navigation
    implementation "androidx.navigation:navigation-ui-ktx:${X_NAVIGATION}"
    implementation "androidx.navigation:navigation-fragment-ktx:${X_NAVIGATION}"
    //X-MultiDex
    implementation "androidx.multidex:multidex:${X_MULTIDEX}"
    //X-Material
    implementation "com.google.android.material:material:${X_MATERIAL}"
    //X-CardView
    implementation "androidx.cardview:cardview:${X_CARD_VIEW}"
    //X-RecyclerView
    implementation "androidx.recyclerview:recyclerview:${X_RECYCLE_RVIEW}"
    //X_ConstraintLayout
    implementation "androidx.constraintlayout:constraintlayout:${X_CONSTRAINT}"
    //X_ViewPager
    implementation "androidx.viewpager2:viewpager2:${X_VIEWPAGER}"
    //X_Paging
    implementation "androidx.paging:paging-runtime-ktx:${X_PAGING}"
    //X-WorkManager
    implementation "androidx.work:work-runtime-ktx:${X_WORK}"
    //X-Room
    implementation "androidx.room:room-ktx:${X_ROOM}"
    implementation "androidx.room:room-runtime:${X_ROOM}"
    kapt "androidx.room:room-compiler:${X_ROOM}"
    testImplementation "androidx.room:room-testing:${X_ROOM}"
    kaptTest "androidx.room:room-compiler:${X_ROOM}"
    //Hilt依赖注入
    implementation "com.google.dagger:hilt-android:${HILT}"
    kapt "com.google.dagger:hilt-android-compiler:${HILT}"
    testImplementation "com.google.dagger:hilt-android-testing:${HILT}"
    kaptTest "com.google.dagger:hilt-android-compiler:${HILT}"
    //Hilt-ViewModel依赖注入
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:${HILT_VIEWMODEL}"
    kapt "androidx.hilt:hilt-compiler:${HILT_VIEWMODEL}"
    //Hilt-WorkManager依赖注入
    implementation "androidx.hilt:hilt-work:${HILT_VIEWMODEL}"
    kapt "androidx.hilt:hilt-compiler:${HILT_VIEWMODEL}"

    //Arouter
    implementation "com.alibaba:arouter-api:${AROUTER_API}"
    kapt "com.alibaba:arouter-compiler:${AROUTER_COMPILER}"
    kaptTest "com.alibaba:arouter-compiler:${AROUTER_COMPILER}"
    //OkHttp
    implementation "com.squareup.okhttp3:okhttp:${OKHTTP}"
    implementation "com.squareup.okhttp3:logging-interceptor:${OKHTTP}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${OKHTTP}"
    testImplementation "com.squareup.okhttp3:okhttp-tls:${OKHTTP}"
    //测试Api
    testImplementation "com.github.andrzejchm.RESTMock:android:${TEST_REST_MOCK}"
    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:${RETROFIT}"
    implementation "com.squareup.retrofit2:converter-gson:${RETROFIT}"
    //Json解析
    implementation "com.google.code.gson:gson:${GSON}"
    //Kotlin Android 工具类Anko Commons
    implementation "org.jetbrains.anko:anko-commons:$ANKO"


    
    //Json动画Lottie
    implementation "com.airbnb.android:lottie:${LOTTIE}"
    //上下拉刷新SmartRefreshLayout
    implementation "com.scwang.smartrefresh:SmartRefreshLayout:${SMART_REFRESH_LAYOUT}"
    //RecyclerView适配器
    implementation "com.github.CymChad:BaseRecyclerViewAdapterHelper:${BASE_RECYCLERVIEW_ADAPTER_HELPER}"
    //日志展示
    implementation "com.orhanobut:logger:${ORHANOBUT_LOGGER}"
    //浏览器内核X5
    implementation "com.tencent.tbs.tbssdk:sdk:${TBSSDK}"
    //键值对存储Mmkv
    implementation "com.tencent:mmkv:${MMKV}"
    //Bug统计Bugly
    implementation "com.tencent.bugly:crashreport:${BUGLY}"
    //热修复Tinker
    implementation "com.tinkerpatch.sdk:tinkerpatch-android-sdk:${TINKER}"
    //内存泄露分析 LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${LEAK_CANARY}"

    //单元测试-Android
    testImplementation "androidx.test:runner:${TEST_X_RUNNER}"
    testImplementation "androidx.test:core:${TEST_X_CORE}"
    testImplementation "androidx.test:rules:${TEST_X_RULES}"
    testImplementation "androidx.test.ext:junit:${TEST_X_JUNIT}"
    testImplementation "androidx.test.espresso:espresso-core:${TEST_X_ESPRESSO}"
    testImplementation "androidx.test.espresso:espresso-intents:${TEST_X_ESPRESSO}"

    //单元测试
    testImplementation "junit:junit:${TEST_JUNIT}"
    testImplementation "org.robolectric:robolectric:${TEST_ROBOLECTRIC}"
    testImplementation "org.mockito:mockito-core:${TEST_MOCKITO}"
    testImplementation "org.mockito:mockito-inline:${TEST_MOCKITO}"//解决Mockito无法mock final class的问题
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:${TEST_MOCKITO_KOTLIN}"
}
